//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================


//Translated & Modified by Start (Discord ID: Start#1356)


prontera,163,175,3	script	Reset Stats & Skills	10295,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "ไม่สามารถ Reset ได้อีก ท่าน Reset เกิน "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+" แล้ว";
		close;
	}
	mes "Reset Stats: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "Reset Skills: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "Reset ทั้งคู่: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "คุณสามารถ Reset ได้ "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+", โปรดคิดให้ถี่ถ้วนก่อนดำเนินการ";
	next;
	set .@i,(select("^FF3355Reset Skills:Reset Stats:Reset ทั้งคู่^000000:ยกเลิก"));
	if(.@i > 3) close;
	if (Zeny < .@Reset[.@i-1]) {
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	if(.@Reset[3]){
		mes "คุณสามารถ Reset ได้ "+callfunc("F_InsertPlural",.@Reset[3],"ครั้ง")+" ต้องการดำเนินการต่อไหม?";
		if(select("ขอคิดก่อน:เอาเลย") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "เรียบร้อย!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
	
OnInit:
	waitingroom "Reset Stats & Skills",0;
	end;
}
